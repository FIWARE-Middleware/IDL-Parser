plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id "com.bmuschko.nexus" version "2.1.1"
    id "me.champeau.gradle.antlr4" version "0.1"
}

//general & artifact properties
group = 'org.fiware.kiara'
version = '0.1.0'
description = "KIARA Advanced Middleware IDL parser"

sourceCompatibility = 1.7
targetCompatibility = 1.7

// fix bootstrap class path not set, when compile to different java version
tasks.withType(JavaCompile) {
    options.bootClasspath = "$System.env.JAVA_HOME/jre/lib/rt.jar"
}


// In this section you declare where to find the dependencies of your project
// see also: http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html#N10621
repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'org.antlr', name: 'antlr4', version:'4.2.2'
    compile group: 'org.antlr', name: 'stringtemplate', version:'3.2'
    testCompile group: 'junit', name: 'junit', version:'4.10'
}

modifyPom {
    project {
        name project.description
        artifactId project.name
        description project.description
        url 'https://github.com/FIWARE-Middleware'
        inceptionYear '2014'
        
        scm {
          connection 'scm:git:https://github.com/FIWARE-Middleware/IDL-Parser.git'
          developerConnection 'scm:git:https://github.com/FIWARE-Middleware/IDL-Parser.git'
          url 'https://github.com/FIWARE-Middleware/IDL-Parser.git'
        }

        licenses {
          license {
            name 'GNU Lesser General Public License, Version 3.0'
            url 'https://www.gnu.org/licenses/lgpl.html'
          }
        }

        developers {
          developer {
            id 'richiprosima'
            name 'Riccardo Gonzalez'
            email 'RicardoGonzalez@eprosima.com'
          }
          developer {
            id 'RafaelLaraAlvarez'
            name 'Rafael Lara Alvarez'
            email 'RafaelLara@eprosima.com'
          }
          developer {
            id 'dmrub'
            name 'Dmitri Rubinstein'
            email 'mach@zhaw.ch'
          }
          developer {
            id 'mach'
            name 'Christof Marti'
            email 'mach@zhaw.ch'
          }
        }
    }
}

extraArchive {
    sources = true
    javadoc = true
    tests = false
}

nexus {
    sign = true
    // To upload to OSSRH comment/remove the following urls
    repositoryUrl = "file:$buildDir/repos/"
    snapshotRepositoryUrl = "file:$buildDir/repos/"
}

// configure antlr4 plugin
antlr4.source = project.file("src/main/antlr4")
antlr4.listener = true
antlr4.visitor = true
antlr4.extraArgs=['-package', 'com.eprosima.idl.parser.grammar']

// make the Java compile task depend on the antlr4 task
compileJava.dependsOn antlr4

// add the generated source files to the list of java sources
sourceSets.main.java.srcDirs += antlr4.output

// add antlr4 to classpath
configurations {
   compile.extendsFrom antlr4
}
